// Define projective spaces
P5<x0,x1,x2,x3,x4,x5> := ProjectiveSpace(FF, 5);
P<a0,a1,a2,a3,a4,a5,b0,b1,b2,b3,b4,b5,c0,c1,c2,c3,c4,c5> := P5 * P5 * P5;
R := CoordinateRing(P);
pr := map<P->P5 | [a0,a1,a2,a3,a4,a5]>;

// Matrix of coordinates
M := Matrix(R, [
    [a0,a1,a2,a3,a4,a5],
    [b0,b1,b2,b3,b4,b5],
    [c0,c1,c2,c3,c4,c5]
]);

// Generate the Plucker coordinates (3x3 minors)
B := Minors(M, 3);

// Define the Plucker embedding into P^19
P19<z012,z013,z014,z015,z023,z024,z025,z034,z035,z045,
    z123,z124,z125,z134,z135,z145,z234,z235,z245,z345> := ProjectiveSpace(FF, 19);

pl := map<P -> P19 | B>;
Gr := Image(pl); // Grassmannian image


// Quadrics for the singular Fermat quadric  x0^2+â‹¯+x4^2 = 0
G1 :=  a0^2 + a1^2 + a2^2 + a3^2 + a4^2;
G2 := 2*(a0*b0 + a1*b1 + a2*b2 + a3*b3 + a4*b4);
G3 := 2*(a0*c0 + a1*c1 + a2*c2 + a3*c3 + a4*c4);
G4 :=  b0^2 + b1^2 + b2^2 + b3^2 + b4^2;
G5 := 2*(b0*c0 + b1*c1 + b2*c2 + b3*c3 + b4*c4);
G6 :=  c0^2 + c1^2 + c2^2 + c3^2 + c4^2;

// Scheme defined by these quadratic forms
WF := Scheme(P, [G1, G2, G3, G4, G5, G6]);

// Define the subvariety of planes contained in the Fermat quadric
W := ReducedSubscheme(pl(WF));

// Define a vector of coordinate functions in P^19
v := [P19.i : i in [1..20]];
x := Vector(v);
